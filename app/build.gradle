apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.neoranga55.cleanguitestarchitecture"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.neoranga55.cleanguitestarchitecture.cucumber.runner.CucumberTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            buildConfigField "String", "TEST_TAGS", "\"${getTestTags()}\""
        }
    }
    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}

// Task to retrieve Cucumber test reports
// The report path in this task depends on the path used in CucumberTestCase.java under @CucumberOptions
// The report path for emulator and real device differ depending on read permissions
// and SD-card presence respectively
gradle.taskGraph.afterTask { task, TaskState state ->
    if ((task == connectedAndroidTest) || (task == connectedCheck)) {
        def reportDir = new File(project.buildDir, "cucumber-reports")
        if (!reportDir.exists()) {
            reportDir.mkdirs()
        }
        println 'Retrieving Cucumber test report to ' + project.buildDir
        def processBuilder = new ProcessBuilder(
                ['adb', 'pull', '/mnt/sdcard/cucumber-reports/', reportDir.getAbsolutePath()])
        processBuilder.start()
    }
}

// Read Cucumber tags from command line like: ./gradlew connectedAndroidTest -Dtags="@login-scenarios,@kitkat"
def getTestTags() {
    return project.hasProperty("tags") ? project.getProperties().get("tags") : ""
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.1'
    // Espresso
    androidTestCompile('com.android.support.test:runner:0.5') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
    }
    // Cucumber
    androidTestCompile('info.cukes:cucumber-android:1.2.4') {
        exclude module: 'cucumber-jvm-deps'
    }
    androidTestCompile('info.cukes:cucumber-picocontainer:1.2.4') {
        exclude module: 'cucumber-jvm-deps'
    }
    androidTestCompile 'info.cukes:cucumber-jvm-deps:1.0.3'
    // Screenshots
    androidTestCompile 'com.squareup.spoon:spoon-client:1.7.1'
}

// Testobject specific part

apply plugin: 'testobject'

project.ext.resolveProp = { prop ->
    if (project.hasProperty(prop)) {
        project.getProperty(prop)
    } else {
        ""
    }
}

// TODO fill with your account's credentials!
testobject {
    baseUrl ""
    username resolveProp("username")
    password resolveProp("password")
    team ""
    app ""
    testSuite 7
    runAsPackage true
}

tasks.all { task ->
    if (task.name.equals("testobjectUpload")) {
        task.doFirst {
            if (!project.hasProperty("username")) {
                throw new GradleException("Please specify testobject user with -Pusername=<yourUserName>")
            }
            if (!project.hasProperty("password")) {
                throw new GradleException("Please specify testobject password with -Ppassword=<yourPassword>")
            }
        }
    }
}